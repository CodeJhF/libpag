function(pag_add_qt6_library)
    cmake_parse_arguments(ARG "" "TARGET;REQUIRED" "MODULES" ${ARGN})

    if (${ARG_REQUIRED})
        foreach(MODULE ${ARG_MODULES})
            find_package(Qt6${MODULE} REQUIRED)
            message(STATUS "Found Qt6${MODULE} at ${Qt6${MODULE}_LIBRARIES}")
            target_link_libraries(${ARG_TARGET} PRIVATE Qt6::${MODULE})
            target_include_directories(${ARG_TARGET} PRIVATE ${Qt6${MODULE}_INCLUDE_DIRS})
        endforeach()
    else ()
        foreach(MODULE ${ARG_MODULES})
            find_package(Qt6${MODULE})
            if (Qt6${MODULE}_FOUND)
                message(STATUS "Found Qt6${MODULE} at ${Qt6${MODULE}_LIBRARIES}")
                target_link_libraries(${ARG_TARGET} PRIVATE Qt6::${MODULE})
                target_include_directories(${ARG_TARGET} PRIVATE ${Qt6${MODULE}_INCLUDE_DIRS})
            else ()
                message(STATUS "Do not Found Qt6${MODULE}, skipping")
            endif ()
        endforeach()
    endif ()
endfunction()

function(pag_add_framework)
    cmake_parse_arguments(ARG "" "TARGET;REQUIRED" "FRAMEWORKS" ${ARGN})

    if (${ARG_REQUIRED})
        foreach(FRAMEWORK ${ARG_FRAMEWORKS})
            find_library(${FRAMEWORK}_LIBRARY ${FRAMEWORK} REQUIRED)
            message(STATUS "Found framework ${FRAMEWORK} at ${${FRAMEWORK}_LIBRARY}")
            target_link_libraries(${ARG_TARGET} PRIVATE "-framework ${FRAMEWORK}")
        endforeach()
    else ()
        foreach(FRAMEWORK ${ARG_FRAMEWORKS})
            find_library(${FRAMEWORK}_LIBRARY ${FRAMEWORK})
            if (${FRAMEWORK}_LIBRARY)
                message(STATUS "Found framework ${FRAMEWORK} at ${${FRAMEWORK}_LIBRARY}")
                target_link_libraries(${ARG_TARGET} PRIVATE "-framework ${FRAMEWORK}")
            else ()
                message(STATUS "Framework ${FRAMEWORK} not found, skipping")
            endif()
        endforeach()
    endif()
endfunction()

function(pag_add_library)
    cmake_parse_arguments(ARG "" "TARGET;REQUIRED" "LIBRARIES" ${ARGN})

    if (${ARG_REQUIRED})
        foreach(LIBRARY ${ARG_LIBRARIES})
            find_library(${LIBRARY}_LIBRARY ${LIBRARY} REQUIRED)
            message(STATUS "Found library ${LIBRARY} at ${${LIBRARY}_LIBRARY}")
            target_link_libraries(${ARG_TARGET} PRIVATE ${${LIBRARY}_LIBRARY})
        endforeach()
    else ()
        foreach(LIBRARY ${ARG_LIBRARIES})
            find_library(${LIBRARY}_LIBRARY ${LIBRARY})
            if (${LIBRARY}_LIBRARY)
                message(STATUS "Found library ${LIBRARY} at ${${LIBRARY}_LIBRARY}")
                target_link_libraries(${ARG_TARGET} PRIVATE ${${LIBRARY}_LIBRARY})
            else ()
                message(STATUS "Library ${LIBRARY} not found, skipping")
            endif()
        endforeach()
    endif()
endfunction()